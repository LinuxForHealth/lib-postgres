buildscript {
    repositories {
		maven {
            url "https://na.artifactory.swg-devops.com/artifactory/wh-imaging-external-maven-virtual"
            credentials {
                username = "${taasArtifactoryUsername}"
                password = "${taasArtifactoryPassword}"
            }
            metadataSources {
                mavenPom()
                artifact()
            }
        }
	}
}

plugins {
    id "ru.vyarus.use-python" version "2.2.0"
    id "org.sonarqube" version "3.0"
}

group = 'com.ibm.watson.health.cdp'
version = "0.0.1"
description = """Postgres async library"""

sonarqube {
    properties {
        property "sonar.sources", pySrcDir
        property "sonar.tests", pyTestDir
        property "sonar.python.coverage.reportPaths", pyXmlCoveragePath
        property "sonar.python.xunit.reportPath", pyTestReportPath
    }
}

// --------
//Whi CAF Python Build Section
python {

    // pythonBinary = 'python3' //local development
    extraIndexUrls = ["https://${taasArtifactoryUsername}:${taasArtifactoryPassword}@na.artifactory.swg-devops.com/artifactory/api/pypi/wh-imaging-pypi-virtual/simple"]
    trustedHosts "https://na.artifactory.swg-devops.com"
    //Python dependencies start
    pip 'asyncpg:0.22.0'
    pip 'pydantic:1.8.2'
    pip 'python-dotenv:0.19.0'

    pip 'whi-caf-lib-configreader:3.2.0'
    pip 'whi-caf-lib-python-logger:3.2.0'
    pip 'pytest-mock:3.6.1'
    //Python dependencies end

    // Change to ./opt/... on Windows OS and not /opt/...
    //envPath = './opt/ibm/whi/app'
    envPath = 'build/venv'
}

task pySetupBuild(type: PythonTask) {
    module = 'pip'
    extraArgs = ['install', 'pytest==6.2.4', 'pytest-cov==2.7.1', 'pytest-asyncio==0.15.1', 'pytest-mock==3.6.1', 'twine==1.14.0', 'flake8==3.7.8', 'black==19.10b0', 'asynctest==0.13.0']       
}

task pyGenerateReqs {
    doLast {
        def newFile = new File("${project.projectDir}/pinned.txt")
        newFile.delete()
        newFile.createNewFile()
        python.modules.each { module ->
            newFile.append(module.replace(":", "==") + "\n")
        }
    }
}

task pySetupBuildProperties {
    doLast {
        def newFile = new File("${project.projectDir}/setup.properties")
        newFile.delete()
        newFile.createNewFile()
        newFile.append("[default]")
        newFile.append("\nproject_name=${project.name}")
        newFile.append("\nproject_version=${project.version}")
        newFile.append("\nproject_srcDir=${pySrcDir}")
    }
}

task pyBuildWheel(type: PythonTask) {
    command = 'setup.py'
    extraArgs = ['bdist_wheel', '-d', pyDistDir, '-k']
}

task pyTestPrep(type: PythonTask) {
    command = 'setup.py'
    extraArgs = ['develop']
}

task pyTest(type: PythonTask) {
    module = 'pytest'
    extraArgs = [pyTestDir, '--junitxml='+pyTestReportPath, '-v', '-rP', '-rx']
}

task pyCoverage(type: PythonTask) {
    module = 'pytest'
    extraArgs = [pyTestDir, '--cov', pySrcDir, '--cov-report=xml:'+pyXmlCoveragePath, '--cov-report=html:'+pyHtmlCoveragePath, '--cov-report=term']
}

task pyClean {
    doLast {
        exec {
            commandLine 'rm', '-rf', 'build'
        }
    }
}

task pyPublishArtifacts(type: PythonTask) {
    doFirst {
        def taasArtifactoryUsername = "${taasArtifactoryUsername}"
        def taasArtifactoryPassword = "${taasArtifactoryPassword}"
        module = 'twine'
        extraArgs = ['upload', '--repository-url', whiPypiRepositoryUrl, '-u', taasArtifactoryUsername, '-p', taasArtifactoryPassword, pyDistDir+'/*.whl']
    }
}

task pyFlake8(type: PythonTask) {
    module = 'flake8'
    extraArgs = [pySrcDir]
}

task pyBlack(type: PythonTask) {
    module = 'black'
    extraArgs = [pySrcDir]
}


task build {}
task test {}
task coverage {}
task uploadArchives {}
task flake8 {}
task clean {}
task black {}

project.tasks.pyTest.dependsOn project.tasks.pySetupBuild
project.tasks.pyTest.dependsOn project.tasks.pyTestPrep
project.tasks.pyTestPrep.dependsOn project.tasks.pySetupBuildProperties
project.tasks.pyCoverage.dependsOn project.tasks.pySetupBuild
project.tasks.pyCoverage.dependsOn project.tasks.pyTestPrep
project.tasks.pyPublishArtifacts.dependsOn project.tasks.pySetupBuild
project.tasks.pyFlake8.dependsOn project.tasks.pySetupBuild

project.tasks.build.dependsOn project.tasks.pySetupBuild
project.tasks.build.dependsOn project.tasks.pySetupBuildProperties
project.tasks.build.dependsOn project.tasks.pyGenerateReqs
project.tasks.build.dependsOn project.tasks.pyTest
project.tasks.build.finalizedBy project.tasks.pyBuildWheel

project.tasks.test.finalizedBy project.tasks.pyTest
project.tasks.coverage.finalizedBy project.tasks.pyCoverage
project.tasks.clean.finalizedBy project.tasks.pyClean
project.tasks.uploadArchives.finalizedBy project.tasks.pyPublishArtifacts
project.tasks.flake8.finalizedBy project.tasks.pyFlake8
project.tasks.black.finalizedBy project.tasks.pyBlack
